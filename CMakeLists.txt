cmake_minimum_required(VERSION 3.16)

project(PluginCore
  VERSION 1.1.0
  LANGUAGES CXX
)

add_compile_definitions( PLUGIN_CORE_VERSION="${PROJECT_VERSION}" )

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

include(GNUInstallDirs)

file(GLOB_RECURSE SRC_FILES
    CONFIGURE_DEPENDS
    ${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/*.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/*.h
)

add_library(PluginCore SHARED ${SRC_FILES})
add_library(d3156::PluginCore ALIAS PluginCore)
set_target_properties(PluginCore PROPERTIES POSITION_INDEPENDENT_CODE ON)

target_include_directories(PluginCore
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/PluginCore>
)

target_compile_definitions(PluginCore PRIVATE $<$<COMPILE_LANGUAGE:CXX>:LOG_NAME="Core">)

# DEB packet

set(PLUGINCORE_CONFIG_INSTALL_DIR ${CMAKE_INSTALL_LIBDIR}/cmake/PluginCore)

install(
  TARGETS PluginCore
  EXPORT PluginCoreTargets
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  COMPONENT runtime
)

install(
  DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src/
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/PluginCore
  COMPONENT dev
  FILES_MATCHING
    PATTERN "*.h"
    PATTERN "*.hpp"
)

install(
  EXPORT PluginCoreTargets
  NAMESPACE d3156::
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/PluginCore
  COMPONENT dev
)

include(CMakePackageConfigHelpers)

configure_package_config_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/PluginCoreConfig.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/PluginCoreConfig.cmake
  INSTALL_DESTINATION ${PLUGINCORE_CONFIG_INSTALL_DIR}
)

write_basic_package_version_file(
  ${CMAKE_CURRENT_BINARY_DIR}/PluginCoreConfigVersion.cmake
  VERSION ${PROJECT_VERSION}
  COMPATIBILITY SameMajorVersion
)

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/PluginCoreConfig.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/PluginCoreConfigVersion.cmake
  DESTINATION ${PLUGINCORE_CONFIG_INSTALL_DIR}
  COMPONENT dev
)

set(CPACK_GENERATOR DEB)
set(CPACK_PACKAGE_NAME "d3156-plugincore")
set(CPACK_PACKAGE_VENDOR "d3156")
set(CPACK_PACKAGE_CONTACT "Smirnov Yaroslav (d3156)")
set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
set(CPACK_DEBIAN_FILE_NAME "DEB-DEFAULT")
set(CPACK_DEB_COMPONENT_INSTALL ON)
set(CPACK_COMPONENTS_ALL runtime dev)
set(CPACK_DEBIAN_RUNTIME_PACKAGE_NAME "d3156-plugincore")
set(CPACK_DEBIAN_RUNTIME_PACKAGE_SECTION "libs")
set(CPACK_DEBIAN_RUNTIME_PACKAGE_DEPENDS "libstdc++6")
set(CPACK_DEBIAN_DEV_PACKAGE_NAME "d3156-plugincore-dev")
set(CPACK_DEBIAN_DEV_PACKAGE_SECTION "libdevel")
set(CPACK_DEBIAN_DEV_PACKAGE_DEPENDS "d3156-plugincore (= ${PROJECT_VERSION})")

include(CPack)
